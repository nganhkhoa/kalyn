(import "../Stdlib.kalyn")

(import "../Assembly.kalyn")

(defn registerToInt (Func Register Int)
  (reg)
  (case reg
    (RAX  0)
    (RCX  1)
    (RDX  2)
    (RBX  3)
    (RSP  4)
    (RBP  5)
    (RSI  6)
    (RDI  7)
    (R8   8)
    (R9   9)
    (R10 10)
    (R11 11)
    (R12 12)
    (R13 13)
    (R14 14)
    (R15 15)
    (RIP 16)))

(public defn compareRegister (Ord Register)
  (r1 r2)
  (compareInt (registerToInt r1)
              (registerToInt r2)))

(public defn compareTemporary (Ord Temporary)
  ((Temporary t1) (Temporary t2))
  (compareInt t1 t2))

(public defn compareVR (Ord VR)
  (r1 r2)
  (case (Pair r1 r2)
    ((Pair (Physical p1) (Physical p2))
     (compareRegister p1 p2))
    ((Pair (Virtual v1) (Virtual v2))
     (compareTemporary v1 v2))
    ((Pair (Physical _) (Virtual _))
     LT)
    ((Pair (Virtual _) (Physical _))
     GT)))
